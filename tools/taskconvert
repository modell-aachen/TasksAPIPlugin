#!/usr/bin/perl

use strict;
use warnings;

use Cwd;
use Getopt::Long;

do '../bin/setlib.cfg';
require Foswiki;
require Foswiki::Func;
require Foswiki::Time;
require Foswiki::Plugins::ActionTrackerPlugin;
require Foswiki::Plugins::ActionTrackerPlugin::Action;
require Foswiki::Plugins::ActionTrackerPlugin::ActionSet;
require Foswiki::Plugins::ActionTrackerPlugin::Format;
require Foswiki::Plugins::TasksAPIPlugin;

# Configuration default values
my %cfg = (
  context    => '',
  defaults   => {},
  dateformat => {},
  force      => 0,
  form       => '',
  help       => 0,
  host       => '',
  map        => {},
  nodry      => 0,
  reindex    => 0,
  source     => [],
  verbose    => 0,
);

# Parse command line parameters
Getopt::Long::GetOptions(
  'context|c=s'      => \$cfg{context},
  'defaults=s%'      => \$cfg{defaults},
  'dateformats|d=s%' => \$cfg{dateformat},
  'force'            => \$cfg{force},
  'form|f=s'         => \$cfg{form},
  'help|h'           => \$cfg{help},
  'host=s'           => \$cfg{host},
  'map|m=s%'         => \$cfg{map},
  'nodry'            => \$cfg{nodry},
  'reindex'          => \$cfg{reindex},
  'source|s=s@'      => \$cfg{source},
  'verbose|v'        => \$cfg{verbose},
) or quit(1,1);

# Check whether to print the help page
quit(0, 1) if $cfg{help};

# collect possible source topics
$cfg{source} = [split(/,/, join(',', @{$cfg{source}}))];

# collect form and date field mappings
collectHashValues('dateformats', 'defaults', 'map');
print $cfg{context} . "\n";
quit(1,1) unless $cfg{context};
quit(1,1) unless $cfg{form};
quit(1,1) unless scalar @{$cfg{source}};

if ($cfg{host}) {
  require Foswiki::Contrib::VirtualHostingContrib::VirtualHost;
  print "Processing host '$cfg{host}'...\n";
  Foswiki::Contrib::VirtualHostingContrib::VirtualHost->run_on($cfg{host}, \&process);
} else {
  process();
}

sub printHelp {
  my $help = <<HELP;

  -c    --context                     Defines the context to use for converted tasks.
                                      One of 'Web.Topic', '\$source' or 'regexp:PATTERN/REPLACEMENT'
                                      Usage:
                                        --context '\$source'
                                        --context 'MyWeb.MyTaskTopic'
                                        --context 'regexp:Tasks//' --source 'MyWeb.ActionTrackerTasks0001'

        --defaults                    List of key/value pairs holding default values for empty/missing form field values.
                                      Usage:
                                        --defaults 'AssignedTo=Team,Type=Information'
                                        --defaults 'AssignedTo=Team' --defaults 'Type=Information'

  -d    --dateformats                 List of key/value pairs defining a parsable date format to use for converting old
                                      date strings to new epoch strings.
                                      Usage:
                                        --dateformats 'created=\$epoch,due=\$epoch'
                                        --dateformats 'created=\$epoch' --dateformats 'due=\$epoch'

                                      See:
                                        http://foswiki.org/System/PerlDoc?module=Foswiki::Time#A_61StaticMethod_61_formatTime_40_36epochSeconds_44_36formatString_44_36outputTimeZone_41_36value

        --force                       Forces 'taskconvert' to create a TasksAPIPlugin task even if the according action
                                      is missing a mapped form field value. See also parameter '--defaults'.

  -f    --form                        The form to use for converted TasksAPI tasks.
                                      Usage:
                                        --form 'MyWeb.MyTaskForm'

  -h    --help                        Shows this help.

        --host                        Runs this script for the given virtual host. [requires VirtualHostingContrib]
                                      Usage:
                                        --host 'virtual.domain.tld'

  -m    --map                         List of key/value pairs to map old ActionTracker fields to new TasksAPI form fields.
                                      Usage:
                                        --map 'creator=Author,created=Created'
                                        --map 'creator=Author' --map 'created=Created'

        --nodry                       By default 'taskconvert' will not commit any changes to TasksAPIPlugin.
                                      Passing this flag will turn off that kind of a "dry run".
                                      Each found Action will be converted to a TasksAPIPlugin task.

        --reindex                     Forces TasksAPI to recreate its internal tasks index.

  -s    --source                      List of webtopics to read ActionTracker actions from.
                                      Usage:
                                        --source 'MyWeb.Topic1,MyWeb.Topic2'
                                        --source 'MyWeb.Topic1' --source 'MyWeb.Topic2'

  -v    --verbose                     Enables verbose logging.

Example usage:
  TODO

HELP
  print $help;
}

sub collectHashValues {
  foreach my $field (@_) {
    my @keys = keys %{$cfg{$field}};
    for my $k (@keys) {
      my $v = $cfg{$field}{$k};
      if ($v =~ /,/ && $v =~ /=/) {
        my @pairs = split(/,/, $v);
        $cfg{$field}{$k} = shift @pairs;
        foreach my $pair (@pairs) {
          my @map = split(/=/, $pair);
          $cfg{$field}{shift @map} = shift @map;
        }
      }
    }
  }
}

sub process {
  new Foswiki('admin');

  Foswiki::Func::setPreferencesValue('tasksapi_suppress_logging', '1');
  foreach my $source (@{$cfg{source}}) {
    print "Processing source topic '$source'...\n" if $cfg{verbose};
    processSource($source);
  }
  Foswiki::Func::setPreferencesValue('tasksapi_suppress_logging', '0');
  Foswiki::Plugins::TasksAPIPlugin::_fullindex() if $cfg{reindex};
  exit 0;
}

sub processSource {
  my $source = shift;

  my ($web, $topic) = Foswiki::Func::normalizeWebTopicName(undef, $source);
  unless (Foswiki::Func::topicExists($web, $topic)) {
    print STDERR "Skipping given source '$web.$topic'. The specified webtopic doesn't exist!\n";
    return;
  }

  my ($fweb, $ftopic) = Foswiki::Func::normalizeWebTopicName(undef, $cfg{form});
  unless ($cfg{form} && Foswiki::Func::topicExists($fweb, $ftopic)) {
    print STDERR "The given form '$cfg{form}' doesn't exist! Aborting!\n";
    exit 1;
  }

  my $ctx = $cfg{context};
  $ctx = "$web.$topic" if $ctx =~ /^\$source$/;
  if (my ($pattern, $replace) = ($ctx =~ /^regexp:(.*?)\/(.*)$/)) {
    my $src = "$web.$topic";
    $src =~ s/$pattern/$replace/;
    $ctx = $src;
  }

  my ($meta, $text) = Foswiki::Func::readTopic($web, $topic);
  my $set = Foswiki::Plugins::ActionTrackerPlugin::ActionSet::load($web, $topic, $text, 1);
  unless (scalar @{$set->{ACTIONS}}) {
    print "No actions found in source topic '$source'!\n" if $cfg{verbose};
    return;
  }

  foreach my $action (@{$set->{ACTIONS}}) {
    next unless $action && ref($action) && $action->isa('Foswiki::Plugins::ActionTrackerPlugin::Action');

    my %task;
    my @missing;
    while (my ($k, $v) = each %{$cfg{map}}) {
      if ($action->{$k}) {
        $task{$v} = $action->{$k};
      } elsif ($action->{unloaded_fields}->{$k}) {
        $task{$v} = $action->{unloaded_fields}->{$k};
      } elsif ($cfg{defaults}{$v}) {
        print "Using default value for mapping $k -> $v\n." if $cfg{verbose};
        $task{$v} = $cfg{defaults}{$v};
      } else {
        push(@missing, $k);
        next;
      }

      if (defined $cfg{dateformat}{$k}) {
        $task{$v} = Foswiki::Time::formatTime($task{$v}, "$cfg{dateformat}{$k}");
      }
    }

    if (scalar @missing) {
      my $m = join(',', @missing);
      my $a = $action->stringify();
      print "Missing keys: '$m' in action:\n$a\n" if $cfg{verbose};
      next unless $cfg{force};
    }

    $task{Context} = $ctx;
    $task{form} = "$fweb.$ftopic";

    if ($cfg{nodry}) {
      my $t = Foswiki::Plugins::TasksAPIPlugin::Task::create(%task);
      print "Created task '$t->{id}'\n" if $cfg{verbose};
    } elsif ($cfg{verbose}) {}
  }
}

sub quit{
  my ($c, $h) = @_;
  printHelp() if $h;
  exit $c;
}

1;
